// https://pest.rs/book/grammars/syntax.html
// @ marks a rule as an atomic. Prevent implicit whitespace and interior rules are silent
// $ marks a rule as a compound atomic. Prevent implicit whitespace but inner tokens are produced as normal
// If a rule is marked as atomic, all of its interior rules will also be atomic
// ! when placed prior to brackets ( like !{ ASCII_ALPHANUMERIC+ } ) will mark a rule as
//    non-atomic, even if it was called from an atomic rule
// _ marks a rule as silent, it will match but not produce rule tokens

// Implicit whitespacing is not on by default, this enables it
Newline = _{ "\n" | "\r\n" }
WHITESPACE = _{ " " | "\t" | Newline }
BlockComment = _{ "/*" ~ (BlockComment | !"*/" ~ ANY)* ~ "*/" }
COMMENT = _{ ("//" ~ (!Newline ~ ANY)*) | BlockComment }
EndOf = _{ ";" }

Main = { SOI ~ Function+ ~ EOI }

// Function
Decorators = !{ "[" ~ (StrPlus | DecoratorKeyValuePair) ~ ("," ~ (StrPlus | DecoratorKeyValuePair))* ~ ","? ~ "]" }
DecoratorKeyValuePair = ${ StrPlus ~ "=" ~ StrPlus }
Function = ${ (Decorators ~ Newline)? ~ "case " ~ StrPlus ~ "()" ~ Block }
Block = !{ WHITESPACE ~ "{" ~ (Statement | Function | Teardown)* ~ "}" ~ EndOf? }
Teardown = { "TEARDOWN" ~ "{" ~ Statement* ~ "}" }
Statement = { (AssignmentExpr | AssertCommand | PrintCommand | Expression ) ~ EndOf }

// ASSIGNMENT STATEMENT
AssignmentExpr = { "var" ~ VariableNameAssignment ~ "=" ~ Expression }

// ASSERT STATEMENT
AssertCommand = { "ASSERT" ~ Negation? ~ AssertSubCommand ~ Value ~ Value ~ QuoteString? }
Negation = { "NOT" }
AssertSubCommand = { "EQUALS" | "GTE" | "GT" | "LTE" | "LT" | "STATUS" | "LENGTH" | "CONTAINS" }

// PRINT STATEMENT
PrintCommand = { "PRINT" ~ Value }

// EXPRESSION STATEMENT
Expression = { HttpCommand | "LITERAL" ~ LiteralValue | "LIST" ~ ListExpression }

// HTTP EXPRESSION
// e.g. PUT /foo?field=5 name="hello" timeout=>60
HttpCommand = { HTTPVerb ~ Path ~ QueryParams? ~ HttpAssignment* ~ HttpHeader* ~ KeyValuePair* }
HTTPVerb = { "GET" | "PUT" | "POST" | "DELETE" }
// e.g. /foo/bar?thing=5&other=50&another=10
Path = ${ PathEndpoint+ }
PathEndpoint = { "/" ~ VariableOrStr+ }
QueryParams = ${ "?" ~ HttpAssignment ~ AdditionalPathArgs* }
AdditionalPathArgs = ${ "&" ~ HttpAssignment }
KeyValuePair = ${ VariableNameAssignment ~ "=>" ~ Value } // repeatable optional key/val like timeout=>60
HttpAssignment = ${ VariableNameAssignment ~ "=" ~ Value }
HttpHeader = ${ VariableNameAssignment ~ ":" ~ Value }

// LIST EXPRESSION
ListExpression = { ListNew | ListCommandExpr }
ListNew = { "NEW" ~ "[" ~ CommaSeparatedValues* ~ Value? ~ "]" }
CommaSeparatedValues = { Value ~ "," }
ListCommandExpr = { ListCommand ~ VariableValue ~ Value? }
ListCommand = { "LENGTH" | "APPEND" | "REMOVE" | "POP" }

// LITERAL
// TODO: Add support for JSON here, see TODO.md
LiteralValue = { QuoteString | Number | Boolean | Null }
// TODO: Will need to add variable support in here
QuoteString = { "\"" ~ AggregatedString ~ "\"" }
Number = { Float | SignedNumber | UnsignedNumber }
Float = @{ "-"? ~ UnsignedNumber ~ "." ~ ASCII_DIGIT+ }
SignedNumber = @{ "-" ~ ASCII_NONZERO_DIGIT ~ ASCII_DIGIT* }
UnsignedNumber = @{ "0" | ASCII_NONZERO_DIGIT ~ ASCII_DIGIT* }
Boolean = { "true" | "True" | "false" | "False" }
Null = { ^"null" }

// VARIABLES
VariableValue = ${ "(" ~ NestedVariable+ ~ ")" }
NestedVariable = { VariableLegalCharacters+ }
VariableLegalCharacters = _{ Str | "." }


// GENERAL USE

Value = ${ LiteralValue | VariableValue }

VariableNameAssignment = { Str+ }

AggregatedString = { StrWithWhitespace+ }
WhiteSpace = @{ " " }
// TODO: Need to support a lot more chars here. String must be able to contain special chars like
//       !?@#$%^&*()[].,"'<>/  and will probably need escape support. Ex, the string
//       "foo bar bash \(asdf\)" has escaped the () so we do not try to parse `asdf` as a variable
// IMPORTANT: When adding this, those additional chars should not be allowed for some uses (like variable names).
//  rather than replacing Str, add a new rule to StrWithWhitespace and maybe change its name to reflect that it should
//  be used to represent a user String
StrWithWhitespace = _{ Str | WhiteSpace }

VariableOrStr = { VariableValue | StrPlus }
StrPlus = { Str+ }
Str = _{ ASCII_ALPHANUMERIC | "-" | "_" }

// Text with multiple variables?
// STRING = { "\"" ~ (TEXT ~ "(" ~ IDENT ~ ")")* ~ TEXT ~ "\"" }
