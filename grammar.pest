// https://pest.rs/book/grammars/syntax.html
// @ marks a rule as an atomic. Prevent implicit whitespace and interior rules are silent
// $ marks a rule as a compound atomic. Prevent implicit whitespace but inner tokens are produced as normal
// If a rule is marked as atomic, all of its interior rules will also be atomic
// _ marks a rule as silent, it will match but not produce rule tokens

// Implicit whitespacing is not on by default, this enables it
WHITESPACE = _{ " " }
COMMENT = _{ ("//" ~ ANY*) | ("/*" ~ (!"*/" ~ ANY)* ~ "*/") }

Main = { SOI ~ Statement ~ EOI }

Statement = { AssignmentExpr | AssertCommand | PrintCommand | Expression }

AssignmentExpr = { "var" ~ VariableNameAssignment ~ "=" ~ Expression }

Expression = { HttpCommand | "LITERAL" ~ LiteralValue | "LIST" ~ ListExpression }

// ASSERT STATEMENT
// This looks like `ASSERT EQUALS (my_var.foo) 5 "FailureMessage"`
AssertCommand = { "ASSERT" ~ Negation? ~ AssertSubCommand ~ Value ~ Value ~ QuoteString? }
Negation = { "NOT" }
AssertSubCommand = { "EQUALS" | "GTE" | "GT" | "LTE" | "LT" | "STATUS" | "LENGTH" | "CONTAINS" }
PrintCommand = { "PRINT" ~ Value }

// HTTP EXPRESSION
// e.g. PUT /foo?field=5 name="hello" timeout=>60
HttpCommand = { HTTPVerb ~ Path ~ HttpAssignment* ~ KeyValuePair* }
HTTPVerb = { "GET" | "PUT" | "POST" | "DELETE" }
// e.g. /foo/bar?thing=5&other=50&another=10
Path = ${ ("/" ~ Str+)+ ~ BeginPathArgs? }
BeginPathArgs = ${ "?" ~ HttpAssignment ~ AdditionalPathArgs* }
AdditionalPathArgs = ${ "&" ~ HttpAssignment }
KeyValuePair = ${ VariableNameAssignment ~ "=>" ~ Value } // repeatable optional key/val like timeout=>60
HttpAssignment = ${ VariableNameAssignment ~ "=" ~ Value }

// LIST EXPRESSION
ListExpression = { ListNew | ListCommandExpr }
ListNew = { "NEW" ~ "[" ~ CommaSeparatedValues* ~ Value? ~ "]" }
CommaSeparatedValues = { Value ~ "," }
ListCommandExpr = { ListCommand ~ VariableValue ~ Value? }
ListCommand = { "LENGTH" | "APPEND" | "REMOVE" | "POP" }

// LITERAL
LiteralValue = { QuoteString | Number | Boolean | Null }
// TODO: Will need to add variable support in here
QuoteString = { "\"" ~ AggregatedString ~ "\"" }
Number = { Float | SignedNumber | UnsignedNumber }
Float = @{ "-"? ~ UnsignedNumber ~ "." ~ ASCII_DIGIT+ }
SignedNumber = @{ "-" ~ ASCII_NONZERO_DIGIT ~ ASCII_DIGIT* }
UnsignedNumber = @{ "0" | ASCII_NONZERO_DIGIT ~ ASCII_DIGIT* }
Boolean = { "true" | "True" | "false" | "False" }
Null = { ^"null" }


// GENERAL USE

VariableValue = ${ "(" ~ NestedVariable+ ~ ")" }

Value = ${ LiteralValue | VariableValue }

VariableNameAssignment = { Str+ }

AggregatedString = { StrWithWhitespace+ }
WhiteSpace = @{ " " }
StrWithWhitespace = _{ Str | WhiteSpace }

Str = _{ ASCII_ALPHANUMERIC | "-" | "_" }

NestedVariable = _{ Str | "." }

// Text with multiple variables?
// STRING = { "\"" ~ (TEXT ~ "(" ~ IDENT ~ ")")* ~ TEXT ~ "\"" }
